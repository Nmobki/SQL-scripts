USE [BKI_IMP_EXP]

GO

DECLARE @CONTRACT VARCHAR(20)
DECLARE @DELIVERY VARCHAR(20)

SET @CONTRACT = '13-376'
SET @DELIVERY = '1'

;

WITH [CTE_TRACE] AS (
SELECT DISTINCT
	LR.[S_CONTRACT_NO] AS [CONTRACT NO]
	,LR.[S_DELIVERY_NAME] AS [DELIVERY]
	,LR.[S_TYPE_CELL] AS [CUSTOMER CODE]
	,LR.[PRODUCTION_ORDER_ID] AS [ROASTING ORDER ID]
	,LG.[PRODUCTION_ORDER_ID] AS [GRINDING ORDER ID]
	,PB.[PRODUCTION_ORDER_ID] AS [PB ORDER ID]
	,PG.[PRODUCTION_ORDER_ID] AS [PG ORDER ID]
FROM [dbo].[PRO_EXP_ORDER_LOAD_R] AS LR
LEFT JOIN [dbo].[PRO_EXP_ORDER_UNLOAD_R] AS ULR
	ON LR.[PRODUCT_ID] = ULR.[S_PRODUCT_ID]
LEFT JOIN [dbo].[PRO_EXP_ORDER_SEND_PB] AS PB
	ON ULR.[S_PRODUCT_ID] = PB.[S_PRODUCT_ID]
LEFT JOIN [dbo].[PRO_EXP_ORDER_LOAD_G] AS LG
	ON ULR.[S_PRODUCT_ID] = LG.[S_PRODUCT_ID]
LEFT JOIN [dbo].[PRO_EXP_ORDER_UNLOAD_G] AS ULG
	ON LG.[PRODUCT_ID] = ULG.[S_PRODUCT_ID]
LEFT JOIN [dbo].[PRO_EXP_ORDER_SEND_PG] AS PG
	ON ULG.[S_PRODUCT_ID] = PG.[S_PRODUCT_ID]
WHERE LR.[S_CONTRACT_NO] = @CONTRACT
	AND LR.[S_DELIVERY_NAME] = @DELIVERY
)
, [CTE_ROAST] AS (
SELECT DISTINCT
	[ROASTING ORDER ID]
FROM [CTE_TRACE]
)
, [CTE_GRIND] AS (
SELECT DISTINCT
	[GRINDING ORDER ID]
FROM [CTE_TRACE]
)
, [CTE_PB] AS (
SELECT DISTINCT
	[PB ORDER ID]
FROM [CTE_TRACE]
)
, [CTE_PG] AS (
SELECT DISTINCT
	[PG ORDER ID]
FROM [CTE_TRACE]
)

SELECT
	'LOAD_R' AS [TABLE]
	,DATEADD(d, DATEDIFF(dd, 0, LR.[RECORDING_DATE] ), 0)  AS [DATE]
	,[LR].[CUSTOMER_CODE]
	,[LR].[DESTINATION]
	,[LR].[PRODUCTION_ORDER_ID]
	,[LR].[ORDER_NAME]
	,SUM([LR].[WEIGHT]) AS [WEIGHT]
	,SUM(CASE
		WHEN [LR].[S_CONTRACT_NO] = @CONTRACT AND [LR].[S_DELIVERY_NAME] = @DELIVERY
			THEN [LR].[WEIGHT]
		ELSE 0
	END) AS [CONTRACT KG]
FROM [dbo].[PRO_EXP_ORDER_LOAD_R] AS [LR]
INNER JOIN [CTE_ROAST]
	ON [LR].[PRODUCTION_ORDER_ID] = [CTE_ROAST].[ROASTING ORDER ID]
GROUP BY
	DATEADD(d, DATEDIFF(dd, 0, LR.[RECORDING_DATE] ), 0)
	,[LR].[CUSTOMER_CODE]
	,[LR].[DESTINATION]
	,[LR].[PRODUCTION_ORDER_ID]
	,[LR].[ORDER_NAME]